apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

Properties localProperties = openProperties("local.properties")
String jksFileName = localProperties.getProperty("sample.storeFile")
File jksFile = jksFileName != null && "" != jksFileName.trim() ? file(jksFileName) : null

android {
    compileSdkVersion COMPILE_SDK_VERSION.toInteger()
    buildToolsVersion "${BUILD_TOOLS_VERSION}"

    defaultConfig {
        applicationId "me.panpf.sketch.sample"
        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        versionCode VERSION_CODE.toInteger()
        versionName "${VERSION_NAME}.${getGitVersion()}"

        ndk {
            abiFilters 'armeabi', 'x86'
        }
    }

    signingConfigs {
        release {
            storeFile jksFile
            storePassword localProperties.getProperty('sample.storePassword')
            keyAlias localProperties.getProperty('sample.keyAlias')
            keyPassword localProperties.getProperty('sample.keyPassword')
        }
    }

    buildTypes {
        debug {
            signingConfig jksFile != null && jksFile.exists() ? signingConfigs.release : signingConfig
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "app"

    productFlavors {
        normal {
            dimension "app"
        }

        // 为了测试没有导入 sketch-gif 时是否可以正常运行
        nogiflib {
            dimension "app"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    aaptOptions {
        noCompress "bmp"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:${ANDROID_SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_LIBRARY_VERSION}"

    implementation "me.xiaopan:pagerslidingtabstrip:${PAGER_SLIDING_TAB_STRIP_VERSION}"
    implementation "me.xiaopan:assemblyadapter:${ASSEMBLY_ADAPTER_VERSION}"

    implementation project(':sketch')
    normalImplementation project(':sketch-gif')

    debugImplementation "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY_ANDROID_VERSION}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_ANDROID_VERSION}"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_ANDROID_VERSION}"

    implementation "org.greenrobot:eventbus:${EVENT_BUS_VERSION}"

    implementation "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

Properties openProperties(String fileName) {
    File propertiesFile = new File(projectDir, fileName)
    if (!propertiesFile.exists()) {
        return new Properties()
    }
    FileInputStream inputStream = new FileInputStream(propertiesFile)
    Properties properties = new Properties()
    properties.load(inputStream)
    inputStream.close()
    return properties
}

def static getGitVersion() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}
