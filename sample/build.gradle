apply plugin: 'com.android.application'

def loadProperties(String fileName) {
    FileInputStream inputStream = new FileInputStream(new File(fileName))
    Properties properties = new Properties()
    properties.load(inputStream)
    inputStream.close()
    for (Map.Entry property in properties) {
        ext[property.key] = property.value
        println(property.key + "=" + property.value)
    }
}

// 加载签名配置文件
String projectPath = projectDir;
loadProperties projectPath + '/local.properties'

android {
    compileSdkVersion 22
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 227
        versionName "2.3.0-beta4"
    }

    signingConfigs {
        release {
            storeFile file(property("sample.storeFile"))
            storePassword property('sample.storePassword')
            keyAlias property('sample.keyAlias')
            keyPassword property('sample.keyPassword')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        normal {
            applicationId "me.xiaopan.sketchsample"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':sketch')
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'me.xiaopan:gohttp:1.2.0'
    compile 'me.xiaopan:androidinjector:1.5.0'
    compile 'me.xiaopan:pagerslidingtabstrip:1.6.0'
    compile 'me.xiaopan:pullrefreshlayout:1.4.0'
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}
